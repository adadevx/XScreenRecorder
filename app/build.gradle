/*
 * Copyright (c) 2016-2018. Vijai Chandra Prasad R.
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see http://www.gnu.org/licenses
 */

apply plugin: 'com.android.application'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    defaultConfig {
        applicationId "com.orpheusdroid.screenrecorder"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 28
        versionName "1.8.8"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    lintOptions {
        abortOnError false
    }
    signingConfigs {
        release
    }
    buildTypes {
        release {
            minifyEnabled true
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            lintOptions {
                disable 'MissingTranslation'
            }
            signingConfig signingConfigs.release
        }
    }

    flavorDimensions "tier"
    productFlavors {
        playstore {
            dimension "tier"
            versionNameSuffix '-playstore'
            buildConfigField "String", "APP_PUB_KEY", getAppKey("app_pub_key")
        }
        fdroid {
            dimension "tier"
            versionNameSuffix '-fdroid'
        }
        playstoreDonate {
            dimension "tier"
            versionNameSuffix '-playstore Donate'
            applicationIdSuffix '.donate'
            buildConfigField "String", "APP_PUB_KEY", getAppKey("app_pub_key_donate")
        }
    }
    sourceSets {
        playstoreDonate {
            java.srcDirs = ['src/main/java', 'src/playstore/java']
            res.srcDirs = ['src/main/res', 'src/playstore/res']
            manifest.srcFile 'src/playstore/AndroidManifest.xml'
        }
    }
}

static def getAppKey(String property){
    Properties properties = new Properties()
    properties.load(new FileInputStream("app_pub_key"))
    return "\"" + properties.getProperty(property) +"\""
}

if (project.hasProperty('RELEASE_STORE_FILE')) {
    println 'Using release keys'
    android.signingConfigs.release.storeFile new File(RELEASE_STORE_FILE)
    android.signingConfigs.release.storePassword RELEASE_STORE_PASSWORD
    android.signingConfigs.release.keyAlias RELEASE_KEY_ALIAS
    android.signingConfigs.release.keyPassword RELEASE_KEY_PASSWORD
} else {
    println 'Release keys not found. Falling back to default config'
    android.buildTypes.release.signingConfig = null
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha2'
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0-alpha4', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.legacy:legacy-support-v13:1.0.0'
    implementation 'ly.count.android:sdk:18.08'
    implementation 'life.knowledge4:k4l-video-trimmer:1.0'
    implementation 'com.github.google:cameraview:3eaeac09a7f'

    playstoreImplementation 'org.solovyev.android:checkout:1.2.1'
    playstoreDonateImplementation 'org.solovyev.android:checkout:1.2.1'

    testImplementation 'junit:junit:4.12'
}
